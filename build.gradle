/*
 * The MIT License
 *
 * Copyright (c) 2014 Marcelo Guimarães <ataxexe@gmail.com>
 *
 * ----------------------------------------------------------------------
 * Permission  is hereby granted, free of charge, to any person obtaining
 * a  copy  of  this  software  and  associated  documentation files (the
 * "Software"),  to  deal  in the Software without restriction, including
 * without  limitation  the  rights to use, copy, modify, merge, publish,
 * distribute,  sublicense,  and/or  sell  copies of the Software, and to
 * permit  persons to whom the Software is furnished to do so, subject to
 * the following conditions:
 *
 * The  above  copyright  notice  and  this  permission  notice  shall be
 * included  in  all  copies  or  substantial  portions  of the Software.
 *                        -----------------------
 * THE  SOFTWARE  IS  PROVIDED  "AS  IS",  WITHOUT  WARRANTY OF ANY KIND,
 * EXPRESS  OR  IMPLIED,  INCLUDING  BUT NOT LIMITED TO THE WARRANTIES OF
 * MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
 * IN  NO  EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY
 * CLAIM,  DAMAGES  OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,
 * TORT  OR  OTHERWISE,  ARISING  FROM,  OUT OF OR IN CONNECTION WITH THE
 * SOFTWARE   OR   THE   USE   OR   OTHER   DEALINGS  IN  THE  SOFTWARE.
 */

apply plugin: 'java'
apply plugin: 'maven'

configurations {
  project.group = "tools.devnull"
  project.version = "0.4"

  project.description = "Kodo is a test framework that helps you creating test " +
      "scenarios with the help of a fluent interface."

  project.targetCompatibility = "1.8"
  project.sourceCompatibility = "1.8"
}

repositories {
  mavenLocal()
  mavenCentral()
}

dependencies {
  compile group: 'org.hamcrest', name: 'hamcrest-core', version: '1.3'
  testCompile group: 'junit', name: 'junit', version: '4.11'
  testCompile group: 'org.mockito', name: 'mockito-all', version: '1.9.5'
}

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives sourcesJar
  archives javadocJar
}

task pom << {
  pom {
    project {
      groupId "${project.group}.${project.name}"
      packaging 'jar'

      name project.name
      description project.description

      url 'https://github.com/ataxexe/kodo'

      licenses {
        license {
          name 'The MIT License'
          url 'http://opensource.org/licenses/MIT'
          distribution 'repo'
        }
      }

      developers {
        developer {
          id 'Ataxexe'
          name 'Marcelo Guimarães'
          email 'ataxexe@devnull.tools'
          organizationUrl 'http://devnull.tools'
          url 'http://entra.la'
        }
      }

      scm {
        connection 'scm:git:git@github.com:ataxexe/kodo.git'
        url 'https://github.com/ataxexe/kodo'
      }

    }
  }.writeTo("$buildDir/pom.xml")
}

task dist(type: Zip,
    dependsOn: [clean, jar, sourcesJar, javadocJar, test, pom]) {
  from "$buildDir/libs"
  from "$buildDir/pom.xml"
  from "changelog.md"
  from "README.md"
}

